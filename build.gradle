apply plugin: "java"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

}

repositories {
    mavenCentral()
}

task testWatcher(type: TestWatcher) {
    testLogging {
        showStandardStreams = true
    }
}

class TestWatcher extends Test {

    @TaskAction
    void executeTests(IncrementalTaskInputs inputs) {
        if (inputs.incremental) {
            def outputDir = this.project.sourceSets['test'].output.classesDir.absolutePath
            def mainDir = this.project.sourceSets['main'].output.classesDir.absolutePath
            this.filter.includePatterns = []
            inputs.outOfDate { InputFileDetails change ->
                def candidate = change.file.absolutePath
                if (candidate.endsWith('.class') && !candidate.startsWith(mainDir)) {
                    candidate = candidate
                            .replace('.class', '')
                            .replace(outputDir, '')
                            .substring(1)
                            .replace(File.separator, '.')
                    this.filter.includePatterns += candidate
                }
            }
        }
        super.executeTests()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion "3.5"
}